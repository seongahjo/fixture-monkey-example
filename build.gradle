buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java'
}

group 'com.navercorp.fixturemonkey.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

apply plugin: "maven-publish"
apply plugin: "signing"

dependencies {
    implementation("org.projectlombok:lombok:1.18.20")
    implementation("javax.validation:validation-api:2.0.1.Final")
    implementation("com.google.code.findbugs:jsr305:3.0.2")
    annotationProcessor("org.projectlombok:lombok:1.18.20")

    testImplementation("com.navercorp.fixturemonkey:fixture-monkey-starter:0.3.1")
    testImplementation("com.navercorp.fixturemonkey:fixture-monkey-jackson:0.3.1")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
    testImplementation("org.assertj:assertj-core:3.18.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.0")
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            repositories {
                maven {
                    def ossrhUsername = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                    def ossrhPassword = project.hasProperty("ossrhPassword") ? ossrhPassword : ""

                    credentials {
                        username ossrhUsername
                        password ossrhPassword
                    }

                    def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                    url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
                }
            }

            pom {
                name = "fixture-monkey"
                description = "The easiest way to generate controllable arbitrary test objects"
                url = "http://github.com/naver/fixture-monkey"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "ah.jo"
                        name = "SeongAh Jo"
                        email = "ah.jo@navercorp.com"
                    }
                    developer {
                        id = "mhyeon-lee"
                        name = "Myeonghyeon Lee"
                        email = "mheyon.lee@navercorp.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/naver/fixture-monkey.git"
                    developerConnection = "scm:git:ssh://github.com/naver/fixture-monkey.git"
                    url = "http://github.com/naver/fixture-monkey"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
